plugins{
    id 'com.jfrog.bintray' version '1.8.4'
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'maven'
}

group = 'com.andre601'
version = '1.0.5'

sourceCompatibility = 1.8

jar{
    baseName = 'JavaBotBlockAPI'
}

repositories{
    mavenCentral()
    jcenter()
}

dependencies{
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.11.0'
    implementation group: 'org.json', name: 'json', version: '20180813'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    implementation group: 'org.jetbrains', name: 'annotations', version: '16.0.2'
    api(group: 'net.dv8tion', name: 'JDA', version: '3.8.3_463'){
        exclude(module: 'opus-java')
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts{
    archives sourcesJar
    archives javadocJar
}

bintray{
    user = bintrayUser
    key = bintrayApiKey
    publications = ['JavaBotBlockAPIRelease']
    pkg{
        repo = 'maven'
        name = 'JavaBotBlockAPI'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/Andre601/JavaBotBlockAPI'
        version{
            name = project.version
            released = new Date()
        }
    }
}

publishing{
    publications{
        JavaBotBlockAPIRelease(MavenPublication){
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}